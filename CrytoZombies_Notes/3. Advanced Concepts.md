### Immutability of Contracts
- After you deploy a contract to Ethereum, it's immutable
	- It cannot be modified or updated again
- The initial code that is deployed to a contract is permanently stored on the blockchain
- For this reason, security is a huge concern for Solidity
	- If there is a security flaw in the inital contract, it cannot be patched
	- Unless you patch the contract and deploy to new address on the blockchain. But you would also need to inform all your users of this change
- This is also a **feature** in smart contracts
	- The code is law.
	- If you read the code of a smart contract and verify it, you can be sure that every time you call a function, it's going to do exactly what the code says to do
	- No one can later change the function

### External Dependencies
- Variables can be hard coded into contracts
	- But what happens if a variable needs to change
	- i.e. an `address` change
- We need to have functions that allow us to update key portions of the contract
- Example

```solidity
contract ChangeVariables{
	address public otherContract = 0x123abcd....

	function setOtherContractAddress(address _newAddress) external {
		otherContract = _newAddress; 
		// there is a security vulnerability here that will be addressed later
	}
}
```

### Ownable Contracts
- The previous contract has a function that is `external`
	- This means that anyone can call it and change the address of our variable that is possibly used elsewhere in the contract
	- We want the ability to update this contract, but we don't want to everyone to be able to update it
- To handle cases like this, one common practice that has emerged is to make contracts `Ownable`
	- Mean they have an owner (you) or has special priviledges

#### OpenZeppelin's `Ownable` Contract
- Below is the `Ownable` contract taken from the **OpenZeppelin** Soldity Library
	- **OpenZepplin** is a library of secure and community-vetted smart contracts that can be used in our own Dapps
```solidity
// SPDX-License-Identifier: MIT

// OpenZeppelin Contracts (last updated v4.7.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
* @dev Contract module which provides a basic access control mechanism, where
* there is an account (an owner) that can be granted exclusive access to
* specific functions.
* 
* By default, the owner account will be the one that deploys the contract. This
* can later be changed with {transferOwnership}.

* This module is used through inheritance. It will make available the modifier
* `onlyOwner`, which can be applied to your functions to restrict their use to
* the owner.
*/

abstract contract Ownable is Context {

	address private _owner;
	
	event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);
	
	/**
	* @dev Initializes the contract setting the deployer as the initial owner.
	*/
	
	constructor() {
		_transferOwnership(_msgSender());
	}
	
	/**
	* @dev Throws if called by any account other than the owner.
	*/
	
	modifier onlyOwner() {
		_checkOwner();
		_;
	}
	
	/**
	* @dev Returns the address of the current owner.
	*/
	
	function owner() public view virtual returns (address) {	
		return _owner;	
	}
	
	/**
	* @dev Throws if the sender is not the owner.
	*/
	
	function _checkOwner() internal view virtual {
		require(owner() == _msgSender(), "Ownable: caller is not the owner");
	}
	
	/**
	* @dev Leaves the contract without owner. It will not be possible to call
	* `onlyOwner` functions anymore. Can only be called by the current owner.
	*
	* NOTE: Renouncing ownership will leave the contract without an owner,
	* thereby removing any functionality that is only available to the owner.	
	*/
	
	function renounceOwnership() public virtual onlyOwner {	
		_transferOwnership(address(0));	
	}
	
	/**	
	* @dev Transfers ownership of the contract to a new account (`newOwner`).	
	* Can only be called by the current owner.
	*/
	
	function transferOwnership(address newOwner) public virtual onlyOwner {
		require(newOwner != address(0), "Ownable: new owner is the zero address");	
		_transferOwnership(newOwner);
	}
	
	/**
	* @dev Transfers ownership of the contract to a new account (`newOwner`).
	* Internal function without access restriction.
	*/
	
	function _transferOwnership(address newOwner) internal virtual {
		address oldOwner = _owner;
		_owner = newOwner;
	
		emit OwnershipTransferred(oldOwner, newOwner);
	}
}
```

- It inherits from a contract called `Context.sol` which is the following 

```solidity
// SPDX-License-Identifier: MIT

// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
* @dev Provides information about the current execution context, including the
* sender of the transaction and its data. While these are generally available
* via msg.sender and msg.data, they should not be accessed in such a direct
* manner, since when dealing with meta-transactions the account sending and
* paying for execution may not be the actual sender (as far as an application
* is concerned).
*

* This contract is only required for intermediate, library-like contracts.
*/

abstract contract Context {

	function _msgSender() internal view virtual returns (address) {
		return msg.sender;

	}

	function _msgData() internal view virtual returns (bytes calldata) {
		return msg.data;
	}

}
```

#### Dissecting the `Ownable` Contract
- A few things that are new

1. `constructor()` is a **constructor**, which is an optional special function that has the same name as the contract. Will get executed only one time, when the contract is first created
2. Function Modifiers: `modifier onlyOwner()`. 
	- These guys are kind of half-functions that are used to modify other functions, usually to check some requirements prior to execution
	- In this case, `onlyOwner` can be used to limit access so **only the owner** of the contract can run this function
	- It is also followed by a `_;` which just continues execution of the function of the requirement check

- Here's what `Ownable` does

1. When a contract is created, its constructor sets the `owner` to `msg.sender` which is the person who deployed the contract
2. It creates the `onlyOwner` modifiers for functions that require only the owner of the contract to execute it
3. It allows you to transfer the contract to a new `owner`

- This is a very common requirement for contracts so most Solidity DApps start with a copy/paste of this `Ownable` contract
- You would just inherit into the contract that requires this

```solidity
contract ChangeVariables is Ownable {
	address public otherContract = 0x123abcd....

	// we set the modifier on the contract
	function setOtherContractAddress(address _newAddress) external onlyOwner {
		otherContract = _newAddress; 
		// now only the owner can change this address
	}
}
```



### Gas
- **The fuel that Ethereum Dapps run on**
- Your users have to pay every time they execute a function on your Dapp using a currency called **gas**
	- Users can buy **gas** with Ether, so your users have to spend ETH in order to execute functions on your Dapp
- Now how much gas is required to execute a function depends on how complex that function's logic is 
- Each individual operation has a **gas cost** based on roughly how much computing resources will be required to perform that operation
	- E.g. writing to storage is more expensive than adding two integers
- The total **gas cost** of the function is the sum of the gas costs of all its individual operations
- Now because of this "feature", **code optimization is extremely important**

### Why is gas necessary?
- Ethereum is like a big slow (but extremely secure) computer
- When you execute a function, every single node on the network needs to run that same function to verify its output
	- These are thousands of nodes
- The creators of Ethereum wanted to ensure that developers would not be able to clog the network with something like infinite loops or intensive computations
	- So users have to pay
	- This is not true for all blockchain networks
		- Loom networks are free I believe?

### Struct packing to save gas
- Remember there are other types of `uint`
	- `uint8`
	- `uint16`
	- `uint32`
- Using smaller types of `uint` in `struct`s can save you some gas
- For example

```solidity
struct NormalStruct{
uint a;
uint b;
uint c;
}

// this one will cost less gas because of struct packing
// you also want to keep similar `uint` types together by clustering
struct MiniMe{
	uint32 a;
	uint32 b;
	uint c;
}
```

